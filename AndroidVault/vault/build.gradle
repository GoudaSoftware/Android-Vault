plugins {
    id 'com.android.library'
    id 'maven-publish'
}

group = "com.goudasoftware"
project.ext.bundle_name = "vault"
version = "1.4.5"

android {
    compileSdk 34
    namespace = group

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 34
        versionCode 1
        versionName project.version
    }
    packagingOptions {
        exclude 'resources.arsc'
        exclude 'AndroidManifest.xml'
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    androidTestImplementation "com.google.android:android-test:4.1.1.4"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from(components["release"])
                groupId = group
                artifactId = project.ext.bundle_name
                version = version

                pom {
                    name = group + "." + project.ext.bundle_name
                    description = "Android-Vault is an encrypted vault for storing sensitive information"
                    url = 'https://github.com/GoudaSoftware/Android-Vault'
                    packaging = "jar"
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = 'GoudaSoftware'
                            name = 'Mr. Banana'
                            email = 'Mr.Banana@goudasoftware.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com:GoudaSoftware/Android-Vault.git'
                        developerConnection = 'scm:git:ssh://github.com:GoudaSoftware/Android-Vault.git'
                        url = 'https://github.com/GoudaSoftware/Android-Vault'
                    }
                }
            }
        }
        repositories {
            maven {
                url layout.buildDirectory.dir("repo")
            }
        }
    }
}